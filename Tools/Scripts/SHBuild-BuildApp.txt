"#", "(C) 2014-2018, 2020-2022 FrankHB.",
"NPLA1 script for building YSLib applications using SHBuild.";

$import! std.strings ++ string-empty? string<-,
$import! std.io load puts,
$import! std.system cmd-get-args env-get,
$import! env_SHBuild_ SHBuild_DirectoryOf_ SHBuild_TrimOptions_;

$def! (prog .args) () cmd-get-args;
$def! SR_Share_NPLA1_ SHBuild_DirectoryOf_ prog;

$def! env_SHBuild_Common_ $provide! (
	$assert-nonempty
	$env-de!
	$redef!
	SHBuild_CheckCXX
	SHBuild_GetPlatformStrings
	SHBuild_Platform_Detect
	build-with-conf-opt
	cons-cmd
	ld-ext-noadjust_
	ld-ext-adjust_win32_subsystem_
	putss
	rmatch?
	safeenv-get
	safeenv-set
	system-ok
	system-or-puts
	system-check
	system-quote-m_
	win32?
) load (++ SR_Share_NPLA1_ "/SHBuild-YSLib-common.txt");

"TODO", "Merge more with logic in %SHBuild-YSLib-build.txt.";
$def! (host-arch host-os) () SHBuild_GetPlatformStrings;
$assert-nonempty host-arch,
$assert-nonempty host-os;

$def! SR_Prefix_ SHBuild_DirectoryOf_ (SHBuild_DirectoryOf_ SR_Share_NPLA1_);
$def! SR_Bin_ ++ SR_Prefix_ "/bin";
$def! SR_Include_ ++ SR_Prefix_ "/include";
$def! SR_Lib_ ++ SR_Prefix_ "/lib";

$defl! arg-first (args) $if (null? args) "" (firstv args);
$defl! arg-rest (args) $if (null? args) () (restv args);

$defl! SHBuild_PrintUsage () putss "Usage: SHBuild -xcmd,RunNPLFile \"" prog
	"\" -- [CONF] [SHBOPT_BASE ...]\n\n"
	"\tBuild application using SHBuild.\n\nCONF\n"
	"\tThe configuration name. If this is missing or empty, show this help."
	" Otherwise, it is identified as the configuration name for the following"
	" build processes. If the configuration name begins with \"debug\" or"
	" environment variable SHBuild_Debug is set as non null value, using debug"
	" configuration; otherwise using release configuration."
	" If the configuration name ends with \"static\","
	" or environment variable SHBuild_Static is set as non null value,"
	" using static configuration;"
	" otherwise using release configuration.\n"
	"\tThe output directory is determined as \".CONF\".\n"
	"\n"
	"SHBOPT_BASE ...\n"
	"\tThe base options remained to pass to SHBuild, including SRCPATH.\n\n"
	"There are several other environment variables to control the build.\n"
	"\n"
	"SHBuild_NoAdjustSubsystem\n"
	"\tIf set to non null, no adjustment for linker arguments would be"
	" performed for MinGW. Otherwise, \"-mwindows\" would be added in "
	" linker command line in release modes.\n";

$def! arg1 arg-first args;
$if (string-empty? arg1) (() SHBuild_PrintUsage)
(
	putss "Found configuration \"" arg1 "\".";
	$def! SHBOPT_BASE arg-rest args;
	$assert-nonempty arg1;
	"XXX", "The argument cannot be empty, so there is no default value.";
	$def! SHBuild_Conf arg1;
	$def! debug rmatch? SHBuild_Conf "debug.*";
	$when (debug) puts "Debug mode turned on by configuration.";
	$def! dynamic $and (not? (rmatch? SHBuild_Conf ".*static"))
		(string-empty? (safeenv-get "SHBuild_Static"));
	$unless (dynamic) puts "Static mode turned on by configuration.";
	"XXX", "No extra configuration arguments are supported yet.";
	$redef! debug
		$or debug (not? (string-empty? (safeenv-get "SHBuild_Debug")));
	$if (debug)
	(
		putss "Use debug configuration \"" SHBuild_Conf "\".";
		$def! SHBuild_YSLib_LibNames "-lYFrameworkd -lYBased"
	)
	(
		putss "Use release configuration \"" SHBuild_Conf "\".";
		$def! SHBuild_YSLib_LibNames "-lYFramework -lYBase"
	);
	$if (win32? host-os)
	(
		$def! SHBuild_YF_SystemLibs "-lgdi32 -limm32 -lquadmath",
		$def! SHBuild_YF_CFlags_freetype ""
	)
	(
		$def! SHBuild_YF_SystemLibs "-Wl,-dy -lxcb -lquadmath";
		$def! SHBuild_YF_CFlags_freetype system-or-puts
			(cons-cmd "pkg-config" "--cflags" "freetype2")
			(++ "-I" (system-quote-m_ SR_Include_))
	);
	$defl! stat-file? (filename)
	(
		"XXX", "Links are not supported.";
		"XXX", "This relies on the executable 'test'";
		system-ok (cons-cmd "test" "-f" filename)
	);
	$if (stat-file? (++ SR_Lib_ "/libfreetype.a"))
		($env-de! SHBuild_YF_Libs_freetype "-lfreetype")
	(
		$def! SHBuild_YF_Libs_freetype system-or-puts
			(cons-cmd "pkg-config" "--libs" "freetype2") "-lfreetype";
		string<- SHBuild_YF_Libs_freetype
			(++ "-Wl,-dy " SHBuild_YF_Libs_freetype)
	);
	$env-de! SHBuild_YF_Libs_FreeImage ++ "-lFreeImage" ($if debug "d" "");
	$def! LIBS
		++ (safeenv-get "LIBS_RPATH") " -L" (system-quote-m_ SR_Lib_);
	$if (dynamic)
	(
		$def! SHBuild_YSLib_Flags SHBuild_TrimOptions_ (++ "-DYF_DLL -DYB_DLL "
			SHBuild_YF_CFlags_freetype " -I" (system-quote-m_ SR_Include_));
		safeenv-set "LIBS" (SHBuild_TrimOptions_
			(cons-cmd LIBS SHBuild_YSLib_LibNames))
	)
	(
		"XXX", "Use 'g++' even when %CXX is 'clang++' to work around the LTO",
			" issue for static executables in Linux.";
		"TODO", "Test for other platforms?";
		$if (eqv? (SHBuild_CheckCXX host-os (safeenv-get "CXX")) "Clang++")
		(
			"XXX", "No prefixes and suffixes of %LD are supported yet.";
			safeenv-set "LD" "g++"
		);
		$def! SHBuild_YSLib_Flags SHBuild_TrimOptions_
			(++ SHBuild_YF_CFlags_freetype " -I"
			(system-quote-m_ SR_Include_));
		$def! SHBuild_YSLib_LibNames cons-cmd SHBuild_YSLib_LibNames
			SHBuild_YF_Libs_FreeImage SHBuild_YF_Libs_freetype
			(++ "-L" (system-quote-m_ SR_Lib_)) SHBuild_YF_SystemLibs;
		safeenv-set "LIBS" (SHBuild_TrimOptions_
			(cons-cmd LIBS "-Wl,-dn" SHBuild_YSLib_LibNames))
	);
	"NOTE", "Configuration (except variable echo) done.";
	$def! SHBuild_BuildDir safeenv-get "SHBuild_BuildDir";
	"NOTE", "Different to %SHBuild-YSLib-build.txt, relative paths are",
		" supported.";
	$unless (string-empty? SHBuild_BuildDir)
		(string<- SHBuild_BuildDir (++ SHBuild_BuildDir "/"));
	putss "Found SHBuild build directory \"" SHBuild_BuildDir "\".";
	$env-de! SHBuild ++ SR_Bin_ "/SHBuild";
	$assert-nonempty SHBuild;
	build-with-conf-opt (++ SHBuild_BuildDir "." SHBuild_Conf) host-arch host-os
		debug dynamic "" #t ($if debug ld-ext-noadjust_
		ld-ext-adjust_win32_subsystem_) ($lambda (#ignore CXXFLAGS SHBOPT .)
		(
			"TODO", "Support precompiled headers?";
			"XXX", "'-fno-lto' is the workaround for MinGW32 release static",
				" builds. See $2020-10 @ doc/Workflow.txt.";
			system-check (++ SHBuild " " (apply cons-cmd (append SHBOPT_BASE
				(list SHBOPT) (list CXXFLAGS) ($if ($and (eqv?
				(SHBuild_Platform_Detect host-os host-arch) "MinGW32")
				(not? dynamic)) (list "-fno-lto") ())
				(list SHBuild_YSLib_Flags))))
		)
	)
);

